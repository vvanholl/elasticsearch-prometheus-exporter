import com.github.mgk.gradle.*

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.elasticsearch.gradle:build-tools:7.2.0" // How can we use ${versions.elasticsearch} here ???
        classpath group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.0'
        classpath group: 'com.sun.xml.bind', name: 'jaxb-core', version: '2.3.0'
        classpath group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.3.0'
        classpath group: 'javax.activation', name: 'activation', version: '1.1.1'
    }
}

plugins {
    id "java"
    id "checkstyle"
    id "co.riiid.gradle" version "0.4.2"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'elasticsearch.esplugin'

// Uncomment if you want to use: System.out.println("Emergency!");
// Logs are found in build/cluster/integTestCluster*/cwd/run.log
//forbiddenApis {
//    ignoreFailures = true
//}

// license of this project
licenseFile = rootProject.file('LICENSE.txt')
// copyright notices
noticeFile = rootProject.file('NOTICE.txt')

// dependency license check needs can be enabled
dependencyLicenses.enabled = false

// thirdparty audit needs can be enabled
thirdPartyAudit.enabled = false

// license header checks can be disabled
licenseHeaders.enabled = true

// No unit tests in this plugin
test.enabled = false

// There are only integration tests
integTestRunner.enabled = true

println "Host: " + java.net.InetAddress.getLocalHost()
println "Gradle: " + gradle.gradleVersion + " JVM: " + org.gradle.internal.jvm.Jvm.current() + " Groovy: " + GroovySystem.getVersion()
println "Build: group: '${project.group}', name: '${project.name}', version: '${project.version}'"
println "Timestamp: " + java.time.Instant.now().atZone(java.time.ZoneId.systemDefault()).toString()

repositories {
    mavenCentral()
    mavenLocal()
}

ext {
    versions = [
        "elasticsearch": version.replaceAll(/\.[0-9]+(|-SNAPSHOT)$/, ""),
        "prometheus"   : "0.6.0",
        "log4j"        : "2.11.1",
        "junit"        : "4.12"
    ]
}

configurations {
    releaseJars {
        extendsFrom runtime
        exclude group: "org.elasticsearch"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "org.apache.logging.log4j"
    }
}

dependencies {
    compile "org.elasticsearch:elasticsearch:${versions.elasticsearch}"
    compile "io.prometheus:simpleclient:${versions.prometheus}"
    compile "io.prometheus:simpleclient_common:${versions.prometheus}"
    compile "org.apache.logging.log4j:log4j-api:${versions.log4j}"
    testCompile "junit:junit:${versions.junit}"
    releaseJars "${project.group}:${project.name}:${project.version}"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked,deprecation"
}

esplugin {
    licenseFile rootProject.file('LICENSE.txt')
    noticeFile rootProject.file('NOTICE.txt')
    name pluginName
    description pluginDescription
    classname pluginClassname
}

integTestCluster {
    numNodes = 2
    clusterName = "PrometheusExporterITCluster"
}

checkstyle {
    configFile = new File(rootDir, "checkstyle.xml")
    toolVersion = "8.2"
}

def getGitCommitHash() {
    def gitFolderPath = "$projectDir/.git/"
    def gitFolder = new File(gitFolderPath + "HEAD");
    if (!gitFolder.exists()) {
        return null
    }

    def head = gitFolder.text.split(":")
    def isCommit = (head.length == 1)

    if (isCommit) {
        return head[0].trim()
    }

    def refHead = new File(gitFolderPath + head[1].trim())
    return refHead.text.trim()
}

github {
    owner = github_owner
    repo = github_repo
    token = github_token
    tagName = version
    targetCommitish = getGitCommitHash()
    name = version
    assets = [
        "build/distributions/${project.name}-${version}.zip"
    ]
}

task release() {
    if (version.endsWith("-SNAPSHOT")) {
        doLast {
            println("SNAPSHOT: Nothing to release !")
        }
    } else {
        dependsOn(["githubRelease"])
    }
}
