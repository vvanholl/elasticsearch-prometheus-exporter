import com.github.mgk.gradle.*

buildscript {
    ext {
        es_version = version.replaceAll(/\.[0-9]+(|-SNAPSHOT)$/, "")
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.elasticsearch.gradle:build-tools:${es_version}"
        classpath group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.0'
        classpath group: 'com.sun.xml.bind', name: 'jaxb-core', version: '2.3.0'
        classpath group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.3.0'
        classpath group: 'javax.activation', name: 'activation', version: '1.1.1'
    }
}

plugins {
    id "java"
    id "checkstyle"
    id "co.riiid.gradle" version "0.4.2"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'elasticsearch.esplugin'
apply plugin: 'elasticsearch.rest-resources'
apply plugin: 'elasticsearch.testclusters'

// Uncomment if you want to use: System.out.println("Emergency!");
// Logs are found in build/testcluster/integTest-*/logs/ folder.
//forbiddenApis {
//    ignoreFailures = true
//}

// license of this project
licenseFile = rootProject.file('LICENSE.txt')
// copyright notices
noticeFile = rootProject.file('NOTICE.txt')

// dependency license check needs can be enabled
dependencyLicenses.enabled = false

// thirdparty audit needs can be enabled
thirdPartyAudit.enabled = false

// license header checks can be disabled
licenseHeaders.enabled = true

// POM validation can be enabled
validateNebulaPom.enabled = false

// No unit tests in this plugin
test.enabled = false

// Temporary disable task :loggerUsageCheck
loggerUsageCheck.enabled = false

// Temporary disable task :testingConventions
testingConventions.enabled = false

println "Host: " + java.net.InetAddress.getLocalHost()
println "Gradle: " + gradle.gradleVersion + " JVM: " + org.gradle.internal.jvm.Jvm.current() + " Groovy: " + GroovySystem.getVersion()
println "Build: group: '${project.group}', name: '${project.name}', version: '${project.version}'"
println "Timestamp: " + java.time.Instant.now().atZone(java.time.ZoneId.systemDefault()).toString()

repositories {
    mavenCentral()
    mavenLocal()
}

ext {
    versions = [
        "elasticsearch": es_version,
        "prometheus"   : "0.8.0",
        "log4j"        : "2.11.1",
        "junit"        : "4.12"
    ]
}

configurations {
    releaseJars {
        extendsFrom runtime
        exclude group: "org.elasticsearch"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "org.apache.logging.log4j"
    }
}

dependencies {
    compile "org.elasticsearch:elasticsearch:${versions.elasticsearch}"
    compile "org.elasticsearch.client:x-pack-transport:${versions.elasticsearch}"
    compile "io.prometheus:simpleclient:${versions.prometheus}"
    compile "io.prometheus:simpleclient_common:${versions.prometheus}"
    compile "org.apache.logging.log4j:log4j-api:${versions.log4j}"
    testCompile (group: 'junit', name: 'junit', version: "${versions.junit}") {
        exclude group:'org.hamcrest' //also included in ES test framework
    }
    releaseJars "${project.group}:${project.name}:${project.version}"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked,deprecation"
}

esplugin {
    licenseFile rootProject.file('LICENSE.txt')
    noticeFile rootProject.file('NOTICE.txt')
    name pluginName
    description pluginDescription
    classname pluginClassname
}

restResources {
  restApi {
    includeCore '*'
  }
}

testClusters.all {
    numberOfNodes = 2

// There does not seem to be any easy way how to setup custom cluster name.

//    This worked in ES 7.4.x, but now results in:
//    A problem occurred evaluating root project 'prometheus-exporter'.
//    > Cannot set the property 'clusterName' because the backing field is final.
//    clusterName = "PrometheusExporterITCluster"

//    This does not work in ES 7.5.x too:
//    Execution failed for task ':integTestRunner'.
//    > Testclusters does not allow the following settings to be changed:[cluster.name] for node{::integTest-0}
//    setting 'cluster.name', 'PrometheusExporterITCluster'
}

licenseHeaders {
  additionalLicense 'APAC2', 'Apache 2.0 License', 'Licensed under the Apache License, Version 2.0'
  approvedLicenses = ['Apache 2.0 License']
}

checkstyle {
    configFile = new File(rootDir, "checkstyle.xml")
    toolVersion = "8.2"
}

def getGitCommitHash() {
    def gitFolderPath = "$projectDir/.git/"
    def gitFolder = new File(gitFolderPath + "HEAD");
    if (!gitFolder.exists()) {
        return null
    }

    def head = gitFolder.text.split(":")
    def isCommit = (head.length == 1)

    if (isCommit) {
        return head[0].trim()
    }

    def refHead = new File(gitFolderPath + head[1].trim())
    return refHead.text.trim()
}

github {
    owner = github_owner
    repo = github_repo
    token = github_token
    tagName = version
    targetCommitish = getGitCommitHash()
    name = version
    assets = [
        "build/distributions/${project.name}-${version}.zip"
    ]
}

task release() {
    if (version.endsWith("-SNAPSHOT")) {
        doLast {
            println("SNAPSHOT: Nothing to release !")
        }
    } else {
        dependsOn(["githubRelease"])
    }
}
